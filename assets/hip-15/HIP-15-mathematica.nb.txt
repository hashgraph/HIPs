(* (c) 2020-2021 Hedera Hashgraph,released under Apache 2.0 license.*)

(* find the HIP-15 checksum for a given Hedera address and ledger ID *)

checksum[ledgerId_, address_] := 
  Module[{h, d, p3, p5, s0, s1, s, sh, c, cp},
   h = Join[ledgerId, {0, 0, 0, 0, 0, 0}];
   d = (ToCharacterCode[address] //. {46 -> 10 + 48}) - 48; (* "." is 46, "0" is 48 *)
   p3 = 26^3;
   p5 = 26^5;
   sd0 = Mod[Total[d[[1 ;; Length[d] ;; 2]]], 11];
   sd1 = Mod[Total[d[[2 ;; Length[d] ;; 2]]], 11];
   sd = Fold[Mod[#1*31 + #2, p3] &, d];
   sh = Fold[Mod[#1*31 + #2, p5] &, h];
   c = Mod[((Mod[Length[d], 5]*11 + sd0)*11 + sd1)*p3 + sd + sh, p5];
   cp = Mod[c*1000003, p5];
   StringJoin @@ 
    FromCharacterCode[(IntegerDigits[cp, 26, 5] + 
       ToCharacterCode["a"][[1]])]];

(* Output the given ledger ID and address along with the calculated \
checksum *)

output[ledgerId_, address_] := 
  Print["ledger: ", ledgerId, "      address: ", address, "-", 
   checksum[ledgerId, address]];

(* Output checksums for all the examples given in HIP-15 *)

addresses = {"0.0.1", "0.0.4", "0.0.5", "0.0.6", "0.0.12", "0.0.123", 
   "0.0.1234567890", "12.345.6789", "1.23.456"};
output[{0}, #] & /@ addresses;
output[{161, 255, 1}, #] & /@ addresses;

(* 
OUTPUT:
ledger:{0} address:0.0.1-dfkxr
ledger:{0} address:0.0.4-cjcuq
ledger:{0} address:0.0.5-ktach
ledger:{0} address:0.0.6-tcxjy
ledger:{0} address:0.0.12-uuuup
ledger:{0} address:0.0.123-vfmkw
ledger:{0} address:0.0.1234567890-zbhlt
ledger:{0} address:12.345.6789-aoyyt
ledger:{0} address:1.23.456-adpbr
ledger:{161,255,1} address:0.0.1-xzlgq
ledger:{161,255,1} address:0.0.4-xdddp
ledger:{161,255,1} address:0.0.5-fnalg
ledger:{161,255,1} address:0.0.6-nwxsx
ledger:{161,255,1} address:0.0.12-povdo
ledger:{161,255,1} address:0.0.123-pzmtv
ledger:{161,255,1} address:0.0.1234567890-tvhus
ledger:{161,255,1} address:12.345.6789-vizhs
ledger:{161,255,1} address:1.23.456-uxpkq
*)
